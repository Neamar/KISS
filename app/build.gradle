buildscript {
    repositories {
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
    id 'net.ltgt.errorprone' version '4.4.0'
}

android {
    buildFeatures {
        buildConfig = true
    }
    defaultConfig {
        applicationId 'fr.neamar.kiss'
        minSdkVersion 21
        compileSdkVersion 35
        targetSdkVersion 35
        versionCode 219
        versionName "3.24.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    lint {
        checkAllWarnings = true
        lintConfig = file('lint.xml')
    }

    testOptions {
        animationsDisabled = false
        unitTests {
            all {
                testLogging {
                    exceptionFormat = "full"
                }
            }
        }
    }

    namespace = 'fr.neamar.kiss'
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.5")

    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'

    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.hamcrest:hamcrest-library:3.0'

    errorprone('com.google.errorprone:error_prone_core:2.46.0')
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableWarningsInGeneratedCode = true
        disable('MissingOverride')
        disable('MissingSummary')
        disable('EmptyBlockTag')
        disable('EmptyCatch')
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
